configurations {
    cucumberRuntime {
        extendsFrom testRuntime
    }
}

dependencies {
	compileOnly group: "com.liferay.portal", name: "com.liferay.portal.kernel", version: "2.0.0"
	compileOnly group: "com.liferay.portal", name: "com.liferay.util.taglib", version: "2.0.0"
	compileOnly group: "javax.portlet", name: "portlet-api", version: "2.0"
	compileOnly group: "javax.servlet", name: "javax.servlet-api", version: "3.0.1"
	compileOnly group: "jstl", name: "jstl", version: "1.2"
	compileOnly group: "org.osgi", name: "org.osgi.core", version: "6.0.0"
	compileOnly group: "org.osgi", name: "org.osgi.service.component.annotations", version: "1.3.0"


	testCompile 'com.liferay.gs:lfrgs-selenium-commons:1.0.4'
    testCompile group: 'junit', name: 'junit', version:'4.12'
    testCompile group: 'org.seleniumhq.selenium', name: 'selenium-api', version:'3.3.1'
    testCompile group: 'org.seleniumhq.selenium', name: 'selenium-java', version:'3.3.1'
    testCompile group: 'org.seleniumhq.selenium', name: 'selenium-remote-driver', version:'3.3.1'
    testCompile group: 'org.seleniumhq.selenium', name: 'selenium-chrome-driver', version:'3.3.1'
    testCompile group: 'org.seleniumhq.selenium', name: 'selenium-firefox-driver', version:'3.3.1'
    testCompile group: 'org.seleniumhq.selenium', name: 'selenium-support', version:'3.3.1'
    testCompile group: 'info.cukes', name: 'cucumber-junit', version:'1.2.5'
    testCompile group: 'info.cukes', name: 'cucumber-java', version:'1.2.5'
    testCompile group: 'com.vimalselvam', name: 'cucumber-extentsreport', version: '1.0.0'
    testCompile group: "com.liferay.arquillian", name: "com.liferay.arquillian.arquillian-container-liferay", version: "1.0.6"
	testCompile group: "org.jboss.arquillian.graphene", name: "graphene-webdriver", version: "2.1.0.Final"
	testCompile group: "org.jboss.arquillian.junit", name: "arquillian-junit-container", version: "1.1.11.Final"
}

startTestableTomcat {
	checkTimeout = 10 * 60 * 1000
}

sourceSets {
    testFunctional {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/testFunctional/java')
        }
        resources.srcDir file('src/testFunctional/resources')
    }
}

configurations {
    testFunctionalCompile.extendsFrom testCompile
    testFunctionalRuntime.extendsFrom testRuntime
}

task cucumberTest(type: Test) {
    
    description 'Runs the Cucumber Tests.'

    outputs.upToDateWhen { false }

    testClassesDir = sourceSets.testFunctional.output.classesDir
    classpath = sourceSets.testFunctional.runtimeClasspath
    outputs.upToDateWhen { false }

    dependsOn ":initBundle", startTestableTomcat, ":apps:angularjs:angularjs-hook:deploy", ":apps:angularjs:angularjs-simple-portlet:deploy"

}

tasks.withType(Test) {
    testLogging {
        events "passed", "skipped", "failed"
        showExceptions true
        exceptionFormat "full"
        showCauses false
        showStackTraces false

        afterSuite { desc, result ->
            if (!desc.parent) {
                def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
                def startItem = '|  ', endItem = '  |'
                def repeatLength = startItem.length() + output.length() + endItem.length()
                println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength))
            }
        }
    }
}

check.dependsOn cucumberTest
